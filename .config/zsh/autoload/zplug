#!/usr/bin/env zsh

local plugins_path="${XDG_CONFIG_HOME:-$HOME/.config}/zsh/plugins"
local compdump_file="${COMPDUMP_FILE:-${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump}"

_zplug-init() {
  local plugin_file
  for plugin_file in "$plugins_path"/*/*.plugin.zsh(.N); do
    source "$plugin_file"
  done
  autoload -Uz compinit && compinit -C -d "$compdump_file"
}

_zsh-re-compinit() {
  [[ -f "$compdump_file" ]] && rm "$compdump_file"
  autoload -Uz compinit && compinit -d "$compdump_file" && zcompile "$compdump_file"
}

_zplug-add-plugin() {
  local plugin_url="$1"
  # Remove the beginning of the URL and any extension (like .git)
  local plugin_name="${plugin_url:t:r}"
  local plugin_path="$plugins_path/$plugin_name"

  if [[ ! -d "$plugin_path" ]]; then
    echo "Installing $plugin_name"
    echo_run git clone "$plugin_url" "$plugin_path"
    [[ "$?" -ne 0 ]] && return 1
    _zsh-re-compinit
  fi
}

case $1 in
  update)
    _zplug-update-plugins "$plugins_path"
    _zsh-re-compinit
    ;;

  add)
    _zplug-add-plugin "${@[2,-1]}"
    ;;

  init)
    _zplug-init
    return "$?"
    ;;

  reinit)
    _zsh-re-compinit
    return "$?"
    ;;

  help | --help | -h)
    _zplug-usage
    ;;

  *)
    _zplug-usage
    return 1
    ;;
esac
