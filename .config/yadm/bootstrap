#!/usr/bin/env zsh

autoload -U colors && colors

# Stop when any command fails
set -e

source "$HOME/.zshrc"

set_local_bin_path() {
  export PATH="$HOME/.local/bin:$PATH"
}

setup_yadm_alt_files() {
  echo_run yadm alt
}

bootstrap_platform() {
  if [[ "$OSTYPE" =~ "darwin" ]]; then
    echo_run source bootstrap_macos
  elif command -v pacman >/dev/null 2>&1; then
    echo_run source bootstrap_arch_linux
  else
    echo_info "No platform bootstrap script for $OSTYPE"
  fi
}

install_submodules() {
  echo_run yadm submodule update --recursive --init
}

install_homebrew_zsh() {
  local zsh_path="$(which zsh)"
  if command -v brew >/dev/null 2>&1; then
    zsh_path="$(brew --prefix)/bin/zsh"
  fi
  # -q be quiet
  # -x match the whole line
  # -F pattern is a plain string
  if grep -qxF "$zsh_path" /etc/shells; then
    echo_info "$zsh_path already in /etc/shells"
  else
    echo_info "Adding $zsh_path to /etc/shells"
    echo_info "$zsh_path" | sudo tee -a /etc/shells >/dev/null
  fi
  if [[ "$SHELL" != "$zsh_path" ]]; then
    safe_run chsh -s "$zsh_path"
  else
    echo_info "User '$USER' already has $zsh_path as their shell"
  fi
}

bootstrap_asdf() {
  if ! command -v asdf >/dev/null 2>&1; then
    echo_info "Installing asdf"
    echo_run git clone https://github.com/asdf-vm/asdf.git ~/.asdf
    cd ~/.asdf
    source asdf.sh
    echo_run git checkout "$(git describe --abbrev=0 --tags)"
  else
    echo_info "asdf already installed. Pulling latest version"
    asdf update
  fi

  echo_run asdf plugin-add golang || true
  echo_run asdf install
  echo_run asdf reshim
}

bootstrap_pyenv() {
  if ! command -v pyenv >/dev/null 2>&1; then
    if [[ -d "$HOME/.pyenv" ]]; then
      echo_run cd ~/.pyenv
      echo_run git init
      echo_run git remote add origin https://github.com/pyenv/pyenv.git
      echo_run git fetch
      echo_run git reset origin/master
      echo_run git checkout --track origin/master
      cd
    else
      echo_run git clone https://github.com/pyenv/pyenv.git ~/.pyenv
    fi
  else
    echo_run cd ~/.pyenv
    if [[ ! -d "$HOME/.pyenv/.git" ]]; then
      echo_run git init
      echo_run git remote add origin https://github.com/pyenv/pyenv.git
      echo_run git fetch
      echo_run git reset origin/master
      echo_run git checkout -- "$HOME/.pyenv"
    fi
    echo_run git branch --set-upstream-to=origin/master master
    echo_run git pull
    cd
  fi
  local version versions
  versions=($(<"$HOME/.pyenv/version"))
  for version in $versions; do
    echo_run pyenv install --skip-existing "$version"
  done
}

bootstrap_rust() {
  if (( ! $+commands[rustup] )); then
    echo_eval 'curl --proto "=https" --tlsv1.2 -sSf https://sh.rustup.rs | sh -s -- --no-modify-path'
    echo_run source $HOME/.cargo/env
  else
    echo_info "Rust is already installed."
    echo_run rustup update
  fi

  echo_eval "rustup completions zsh > $HOME/.config/zsh/completions/_rustup"
  echo_eval "rustup completions zsh cargo > $HOME/.config/zsh/completions/_cargo"
}

bootstrap_alacritty() {
  echo_info "Compiling alacritty terminfo file"
  TMP_DIR=$(mktemp -d)
  cd $TMP_DIR
  echo_run curl -O https://raw.githubusercontent.com/alacritty/alacritty/master/extra/alacritty.info
  echo_run tic -xe alacritty,alacritty-direct alacritty.info
  echo_run curl -O https://raw.githubusercontent.com/alacritty/alacritty/master/extra/completions/_alacritty
  echo_run mv _alacritty "$HOME/.config/zsh/completions/_alacritty"
  cd
  rm -rf $TMP_DIR
}

bootstrap_tmux() {
  echo_info "Installing tmux terminfo file"
  echo_run tic -x $XDG_CONFIG_HOME/tmux/tmux.terminfo

  if command -v tmux >/dev/null 2>&1; then
    echo_info "Installing, updating, and cleaning tmux plugins"

    if [ ! -d "${XDG_CONFIG_HOME:-$HOME/.config}/tmux/tpm" ]; then
      echo_run git clone https://github.com/tmux-plugins/tpm \
        "${XDG_CONFIG_HOME:-$HOME/.config}/tmux/tpm"
    fi

    echo_run ${XDG_CONFIG_HOME:-$HOME/.config}/tmux/tpm/bin/install_plugins
    echo_run ${XDG_CONFIG_HOME:-$HOME/.config}/tmux/tpm/bin/update_plugins all
    echo_run ${XDG_CONFIG_HOME:-$HOME/.config}/tmux/tpm/bin/clean_plugins
  else
    echo_info "tmux not installed."
  fi
}

decrypt_yadm_files() {
  if [[ ! -f "$HOME/.ssh/id_rsa" ]]; then
    echo_run yadm decrypt
    echo_run chmod 700 ~/.ssh
    echo_run chmod 600 ~/.ssh/id_rsa
    echo_run chmod 644 ~/.ssh/id_rsa.pub
    echo_run gpg --import ~/.gnupg/.exports/pgp-public-keys.asc
    echo_run gpg --import ~/.gnupg/.exports/pgp-private-keys.asc
    echo_run gpg --import-ownertrust ~/.gnupg/.exports/pgp-ownertrust.asc
  else
    echo_info "The file ~/.ssh/id_rsa already exists."
    echo_info "Skipping decryption. Run 'yadm decrypt && gpg_restore' if necessary."
  fi
}

update_yadm_url_to_ssh() {
  # We now have ssh keys decrypted, so can update the url
  # to be the ssh url
  echo_info "Updating the yadm repo origin URL"
  echo_run yadm remote set-url origin "git@github.com:georgewitteman/dotfiles.git"
}

yadm_pull_rebase() {
  echo_run yadm gitconfig pull.rebase true
}

remove_zcompdump() {
  if [[ -f "$HOME/.cache/zcompdump" ]]; then
    echo_run rm "$HOME/.cache/zcompdump"
  else
    echo_info "No zcompdump file to remove"
  fi
}

reminders() {
  echo
  echo_info "REMINDERS:"
  echo_info " - Don't forget to set yadm class for work computer"
  echo_info "     $ yadm config local.class work"
}

sudo echo "$SHELL" > /dev/null
if [[ $status -ne 0 ]]; then
  echo_info "We need sudo to do some stuff"
  return 1
fi

set_local_bin_path
setup_yadm_alt_files
bootstrap_platform
decrypt_yadm_files
update_yadm_url_to_ssh
yadm_pull_rebase
install_submodules
install_homebrew_zsh
bootstrap_asdf
bootstrap_pyenv
bootstrap_rust
bootstrap_alacritty
bootstrap_tmux

# Keep at end
remove_zcompdump

# Keep last
reminders
