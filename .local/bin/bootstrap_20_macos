#!/usr/bin/env zsh

set -e
source "$HOME/.zshrc"

if [[ ! "$OSTYPE" =~ "darwin" ]]; then
  echo_warn "Not on macOS. Skipping macOS."
  exit
fi

# install homebrew if it's missing
if (( ! $+commands[brew] )); then
  echo_info "Installing homebrew"
  echo_eval '/usr/bin/ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"'
fi

echo_run command brew update

# echo_run command brew upgrade
echo_run command brew outdated
echo_warn "Skipping 'brew upgrade' since it's kind of slow."

local brew_list=($(command brew list -1))

if (( $brew_list[(Ie)universal-ctags] )); then
  echo_ok "universal-ctags already installed"
else
  echo_run command brew install --HEAD universal-ctags/universal-ctags/universal-ctags
fi

if (( $brew_list[(Ie)vim] )); then
  echo_ok 'vim already installed'
else
  echo_run command brew install --HEAD vim
fi

local formulas=(
  awscli
  bat
  curl
  diff-so-fancy
  exa
  fd
  fzf
  git
  gnupg
  httpie
  hyperfine
  jq
  neovim
  parquet-tools
  pgcli
  pinentry-mac
  ripgrep
  rsync
  shellcheck
  tealdeer
  terraform
  tmux
  trash
  tree
  wget
  yadm
  zsh
)

local formulas_to_install=()
local installed_formulas=()
local formula
for formula in $formulas; do
  if (($brew_list[(Ie)$formula])); then
    installed_formulas+=("$formula")
  else
    formulas_to_install+=("$formula")
  fi
done

if [[ "${#installed_formulas}" -ne 0 ]]; then
  echo_ok "Already installed: $installed_formulas"
fi

if [[ "${#formulas_to_install}" -ne 0 ]]; then
  echo_run command brew install $formulas_to_install
else
  echo_ok "No more formulas to install."
fi
