#!/usr/bin/env zsh

local plugins_path="${XDG_CONFIG_HOME:-$HOME/.config}/zsh"
local compdump_file="${ZDOTDIR:-${HOME}}/.zcompdump"

__zplug-init() {
  local plugin_file
  for plugin_file in "$plugins_path"/*/*.plugin.zsh(N); do
    source "$plugin_file"
  done
  autoload -Uz compinit && compinit -C -d "$compdump_file"
}

__zplug-add-plugin() {
  local plugin_url="$1"
  # Remove the beginning of the URL and any extension (like .git)
  local plugin_name="${plugin_url:t:r}"
  local plugin_path="$plugins_path/$plugin_name"

  if [[ ! -d "$plugin_path" ]]; then
    echo "Installing $plugin_name"
    echo-run git clone "$plugin_url" "$plugin_path"
    [[ "$?" -ne 0 ]] && return 1
    __zplug-reinit "$compdump_file"
  fi
}

case $1 in
  clean)
    __zplug-clean "$compdump_file"
    ;;

  update)
    __zplug-update-plugins "$plugins_path"
    __zplug-reinit "$compdump_file"
    ;;

  add)
    __zplug-add-plugin "${@[2,-1]}"
    ;;

  init)
    __zplug-init
    ;;

  reinit)
    __zplug-reinit "$compdump_file"
    ;;

  help | --help | -h)
    __zplug-usage
    ;;

  *)
    __zplug-usage
    return 1
    ;;
esac
