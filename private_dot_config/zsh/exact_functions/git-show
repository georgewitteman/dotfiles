#!/usr/bin/env zsh

if ! in-git-repo; then
  echo-err "Not in a git repository."
  return 128
fi

local commit_fmt=()
local newln="%n"
local ref="$(git show "$@" --quiet --format="%H")"

if ! git rev-parse "$ref" >/dev/null 2>&1; then
  echo "Invalid ref: $ref"
  return 1
fi

local refs=($(
  git show-ref --dereference |
    grep "$(git rev-parse "$ref")" |
    cut -d ' ' -f 2
))
local tags=(${${${(M)refs:#refs\/tags\/*}/#refs\/tags\//}/%\^\{\}/})
local heads=(${${(M)refs:#refs\/(heads|remotes)\/*}/#refs\/(heads|remotes)\//})
local diff_url="$(git show --quiet --format="%b" $ref | grep "^Differential Revision: " | cut -d ' ' -f 3)"

local header="======== git show --format=\"â€¦\" $@ ========"
local footer="$(printf '=%.0s' {1..${#header}})"

commit_fmt+=("$header")
commit_fmt+=("   Hash: %h")
commit_fmt+=("   %C(cyan)Tags:%Creset %C(bold)${(j:, :)tags}%Creset")
commit_fmt+=("  %C(red)Heads:%Creset ${(j:, :)heads}")
[[ -n "$diff_url" ]] && commit_fmt+=("   %C(yellow)Diff:%Creset $diff_url")
commit_fmt+=(" %C(green)Author:%Creset %an <%ae>")
commit_fmt+=("   %C(blue)Date:%Creset %cd (%ar)")
commit_fmt+=("%C(magenta)Subject:%Creset %C(bold)%s%Creset")
commit_fmt+=("$footer")

# For some reason piping makes git show run way faster (~500ms -> ~20ms)
git show --color=always --format="${(j:%Creset%n:)commit_fmt}" "$@" | cat
