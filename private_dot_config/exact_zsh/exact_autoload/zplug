#!/usr/bin/env zsh

local plugins_path="${XDG_CONFIG_HOME:-$HOME/.config}/zsh/plugins"
local compdump_file="${COMPDUMP_FILE:-${XDG_CACHE_HOME:-$HOME/.cache}/zcompdump}"

__zplug-init() {
  local plugin_file
  for plugin_file in "$plugins_path"/*/*.plugin.zsh(.N); do
    source "$plugin_file"
  done
  autoload -Uz compinit && compinit -C -d "$compdump_file"
}

__zplug-re-compinit() {
  [[ -f "$compdump_file" ]] && rm "$compdump_file"
  autoload -Uz compinit && compinit -d "$compdump_file" && zcompile "$compdump_file"
}

__zplug-add-plugin() {
  local plugin_url="$1"
  # Remove the beginning of the URL and any extension (like .git)
  local plugin_name="${plugin_url:t:r}"
  local plugin_path="$plugins_path/$plugin_name"

  if [[ ! -d "$plugin_path" ]]; then
    echo "Installing $plugin_name"
    echo_run git clone "$plugin_url" "$plugin_path"
    [[ "$?" -ne 0 ]] && return 1
    __zplug-re-compinit
  fi
}

case $1 in
  update)
    __zplug-update-plugins "$plugins_path"
    __zplug-re-compinit
    ;;

  add)
    __zplug-add-plugin "${@[2,-1]}"
    ;;

  init)
    __zplug-init
    return "$?"
    ;;

  reinit)
    __zplug-re-compinit
    return "$?"
    ;;

  help | --help | -h)
    __zplug-usage
    ;;

  *)
    __zplug-usage
    return 1
    ;;
esac
